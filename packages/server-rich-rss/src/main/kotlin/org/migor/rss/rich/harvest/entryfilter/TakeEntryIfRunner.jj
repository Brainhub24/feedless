options {
    STATIC = false;
    IGNORE_CASE = true;
}

PARSER_BEGIN(TakeEntryIfRunner)

package org.migor.rss.rich.harvest.entryfilter.generated;

import org.apache.commons.lang3.StringUtils;
import org.migor.rss.rich.database.model.Article;
import java.util.Arrays;
import org.slf4j.Logger;
import org.slf4j.LoggerFactory;

public class TakeEntryIfRunner {

  private final Logger log = LoggerFactory.getLogger(TakeEntryIfRunner.class);

//  javacc *.jj
//  javac *.java
//  java FilterExprParser < input.txt
//
//  public static void main(String[] args) throws Exception {
//    Scanner scanner = new Scanner(System.in);
//
//    while(scanner.hasNextLine()) {
//      String line = scanner.nextLine();
//      TakeEntryIfRunner parser = new TakeEntryIfRunner(new ByteArrayInputStream(line.getBytes()));
//      System.out.println(line + " -> " + parser.takeIf(null));
//    }
//  }

  String strField(String fieldName, Article entry) {
    switch (fieldName) {
      case "content": return entry.getContent();
      case "title": return entry.getTitle();
    }
    throw new RuntimeException("Field '" + fieldName + "' is not supported");
  }

  double numField(String fieldName, Article entry) {
    switch (fieldName) {
      case "linkCount": return (double) entry.linkCount();
      case "score": return entry.getScore();
    }
    throw new RuntimeException("Field '" + fieldName + "' is not supported");
  }

  int wordCount(String val) {
    return (int) Arrays.stream(StringUtils.split(val, " ,;:'.!?"))
//      .filter(StringUtils::isNotBlank)
      .count();
  }

  int sentenceCount(String val) {
    return (int) Arrays.stream(StringUtils.split(val, ";.!?"))
//        .filter(StringUtils::isNotBlank)
        .count();
  }

}



PARSER_END(TakeEntryIfRunner)

SKIP : { " "|"\n"|"\r"|"\r\n" }
TOKEN : { <STR_FIELD : "title" | "content" | "domain" | "author" > }
TOKEN : { <NUM_FIELD : "score" | "linkCount" > }
TOKEN : { <SENTENCES : "sentences" > }
TOKEN : { <WORDS : "words" > }
TOKEN : { <LENGTH : "len" > }
TOKEN : { <GT : ">" > }
TOKEN : { <LT :  "<" > }
TOKEN : { <BOOL : "true" | "false" > }
TOKEN : { <AND : "&&" > }
TOKEN : { <OR : "||" > }
TOKEN : { <QUOTE : "'" > }
TOKEN : { <LBRAKE : "(" > }
TOKEN : { <RBRAKE : ")" > }
TOKEN : { <COMMA : "," > }
TOKEN : { <NOT : "not" > }
TOKEN : { <ENDSWITH : "endsWith" > }
TOKEN : { <NUMBER : (["0"-"9"])+ > }
TOKEN : { <STRING : (["a"-"z","?"])+ > }

boolean takeIf(Article entry) throws Exception :
{ boolean b; }
{
    b = BoolExpr(entry)
    <EOF>
   { return b; }
}

boolean BoolExpr(Article entry) throws Exception :
{  Token t; boolean result; boolean boolA, boolB; }
{
  t = <BOOL>
  {  boolA = Boolean.parseBoolean( t.image );  }
  (
        <AND>
        boolB = BoolExpr(entry)
        {  return boolA && boolB; }

        |

        <OR>
        boolB = BoolExpr(entry)
        {  return boolA || boolB; }

        |

        {  return boolA;  }
  )

  |

  <NOT>
  <LBRAKE>
  {  result = !BoolExpr(entry);  }
  <RBRAKE>
  { return result; }

  |

  result = StringExpr(entry)
  { return result; }

  |

  result = NumberExpr(entry)
  { return result; }
}

boolean StringExpr(Article entry) throws Exception :
{  String strA, strB; boolean result; }
{
  <ENDSWITH>
  <LBRAKE>
  strA = StringValue(entry)

  <COMMA>
  strB = StringValue(entry)
  {  result = strA.endsWith(strB);  }
  <RBRAKE>
  { return result; }

}

String StringValue(Article entry) throws Exception :
{  Token t; }
{
  <QUOTE>
  t = <STRING>
  <QUOTE>
  {  return t.image;  }

  |

  t = <STR_FIELD>
  {  return strField( t.image, entry );  }

}


boolean NumberExpr(Article entry) throws Exception :
{  double a, b; String str; }
{
    a = Number(entry)
    (
      <GT>
      b = Number(entry)
      {  return a > b; }

      |

      <LT>
      b = Number(entry)
      {  return a < b; }
    )
}


double Number(Article entry) throws Exception :
{  Token t; int i; }
{
  t = <NUMBER>
  {  return Double.parseDouble( t.image );  }

  |

  t = <NUM_FIELD>
  {  return numField( t.image, entry );  }

  |

  i = len(entry)
  {  return (double)i; }

  |

  i = words(entry)
  {  return (double)i; }

  |

  i = sentences(entry)
  {  return (double)i; }

}

int len(Article entry) throws Exception :
{  String str; }
{
    <LENGTH>
    <LBRAKE>
    str = StringValue(entry)
    <RBRAKE>
    { return str.length(); }

}

int words(Article entry) throws Exception :
{  String str; }
{
    <WORDS>
    <LBRAKE>
    str = StringValue(entry)
    <RBRAKE>
    { return wordCount(str); }

}

int sentences(Article entry) throws Exception :
{  String str; }
{
    <SENTENCES>
    <LBRAKE>
    str = StringValue(entry)
    <RBRAKE>
    { return sentenceCount(str); }

}
